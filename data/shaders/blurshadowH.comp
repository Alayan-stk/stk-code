uniform sampler2D source;
uniform layout(r32f) restrict writeonly image2D dest;
uniform vec2 pixel;
uniform float sigma;

// Gaussian separated blur with radius 6.

layout (local_size_x = 8, local_size_y = 8) in;

shared float local_src[8 + 2 * 6][8];

void main()
{
    int x = int(gl_LocalInvocationID.x), y = int(gl_LocalInvocationID.y);
    ivec2 iuv = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);
    vec2 uv_m = (iuv - ivec2(6, 0)) * pixel;
    vec2 uv = iuv * pixel;
    vec2 uv_p = (iuv + ivec2(6, 0)) * pixel;

    local_src[x][y] = texture(source, uv_m).x;
    local_src[x + 6][y] = texture(source, uv).x;
    local_src[x + 12][y] = texture(source, uv_p).x;

    barrier();

    float g0, g1, g2;
    g0 = 1.0 / (sqrt(2.0 * 3.14) * sigma);
    g1 = exp(-0.5 / (sigma * sigma));
    g2 = g1 * g1;
    float sum = local_src[x + 6][y] * g0;
    g0 *= g1;
    g1 *= g2;
    for (int i = 1; i < 6; i++) {
        sum += local_src[6 + x - i][y] * g0;
        sum += local_src[6 + x + i][y] * g0;
        g0 *= g1;
        g1 *= g2;
    }

    imageStore(dest, iuv, vec4(sum));
}
